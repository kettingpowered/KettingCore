plugins {
    id 'java-library'
    id 'net.nemerosa.versioning' version '3.0.0'
}

apply plugin: 'maven-publish'
group = 'org.kettingpowered'

repositories {
    maven {
        name 'c0d3m4513r'
        url 'https://reposilite.c0d3m4513r.com/Forge'
    }
    maven {
        name 'MinecraftForge'
        url 'https://maven.minecraftforge.net/'
    }
}

dependencies {
    api 'net.fabricmc:mapping-io:0.6.1'
    api 'org.slf4j:slf4j-api:1.8.0-beta4'
    implementation 'org.spongepowered:mixin:0.8.5'
    implementation 'org.jetbrains:annotations:24.0.0'
    implementation 'it.unimi.dsi:fastutil:8.5.12'

    //ASM
    implementation 'org.ow2.asm:asm:9.7'
    implementation 'org.ow2.asm:asm-tree:9.7'
    implementation 'org.ow2.asm:asm-util:9.7'
    implementation 'org.ow2.asm:asm-commons:9.7'
    implementation 'org.ow2.asm:asm-analysis:9.7'
}

jar {
    manifest {
        attributes 'Specification-Title': 'Kettingpowered',
            'Specification-Vendor': 'Kettingpowered',
            'Implementation-Title': 'KettingCore',
            'Implementation-Version': System.getenv("VERSION") ?: 'dev-ver',
            'Implementation-Vendor': 'Kettingpowered'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
      myPublication(MavenPublication) {
        from components.java
        artifact(sourcesJar) {
            classifier "sources"
            extension "jar"
        }
        artifact(javadocJar) {
            classifier "javadoc"
            extension "jar"
        }

        version = System.getenv("VERSION")
        pom {
          name = "KettingCore"
          description = "An API supported across all Ketting Versions"
          url = "https://github.com/kettingpowered/KettingCore"
          licenses {
            license {
              name = "MIT License"
              url = "http://www.opensource.org/licenses/mit-license.php"
            }
          }
          developers {
            developer {
              id = "justred23"
              name = "JustRed23"
            }
          }
          scm {
            connection = "scm:git:https://github.com/kettingpowered/KettingCore.git"
            url = "https://github.com/kettingpowered/KettingCore"
          }
        }
      }
    }
    repositories {
       maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/kettingpowered/KettingCore")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
       }
        maven {
            name = 'kettingRepo'
            credentials {
                username = System.getenv("KETTINGUSERNAME")
                password = System.getenv("KETTINGPASSWORD")
            }
            url = "https://reposilite.c0d3m4513r.com/Ketting/"
        }
    }
}
